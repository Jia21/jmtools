#!/usr/bin/perl

use warnings;
use strict;
use File::Basename;
use Getopt::Long;
use Pod::Usage;

=head1 NAME

snpBed2DomainPos

=head1 SYNOPSIS

 snpBed2DomainPos [options] -e <ensembl-file> <smartDomain-file>

  -h help
  
  -e mandatory; <ensembl-file> should be tab-delimited with the following headers:
  EnsemblProteinID
  genomicCodingStart (1-based)
  genomicCodingEnd (stop codon usually included)
  -- MUST BE sorted by proteinID, chr, coding Start and end in this order
  -- sort -k5,5n -k3,3d  -k8,8n -k9,9n 
  
  <smartDomain-file> file extracted from Ensembl about protein domains (from Suganthi)
  Chromosome:1...
	SM00454 540-607 (ENSP00000342313)          
	(smartDomainID  residuePos EnsemblProteinID)
	
	convert format to tsv via smartAApos2tsv
	chr     smartID start   end     EnsemblProteinID
	chr1    SM00454 540     607     ENSP00000342313
 
  OUTPUT:
  col1: chr
  col2: gStart 1-based
  col3: gEnd   1-based
  col4: motif annotation
  
  Example:
     snpBed2DomainPos -e ensembl2coding_ensembl73.proteinIDs.genomicPos.txt allchromosomes.ens73.alldomainfeatures.smart.txt > jm.bed
  
=head1 DESCRIPTION

=cut

#option variables
my $help;
my $ensemblFile = '';

#initialize options
Getopt::Long::Configure ('bundling');

if(!GetOptions ('h'=>\$help, 'e=s'=>\$ensemblFile) || scalar(@ARGV)!=1)
{
    if ($help)
    {
        pod2usage(-verbose => 2);
    }
    else
    {
        pod2usage(1);
    }
}

## read ensembl file
open (ENSEM, $ensemblFile) || die "Cannot open $ensemblFile: $!";


## output file
#my($name, $path, $ext) = fileparse($ifile, '\..*');
#my $ofile = "$name.out";
#open (OUTPUT, ">$ofile") || die "Cannot open $ofile: $!";

# variables
my $headerProcessed = 0;
my $domainCtr = 0;
my %header2col;
my %proteinID2aaPos2gPos;
my %proteinID2geneID;
my %proteinID2strand;
my $aaPosCtr = 1;
my $ctr = 0;
my $offset = 0;
	
## read ensembl file
LINE: while (<ENSEM>)
{
	s/\r?\n?$//;
	my @fields = split(/\t/, $_);
	chomp @fields; 
	
	if($headerProcessed == 0)
	{
		ITER: for my $desired ('EnsemblGeneID', 'EnsemblProteinID', 'EnsemblExonID', 
													 'chr', 'genomicCodingStart', 'genomicCodingEnd',
													 'strand')
		{
			for (my $i=0;$i<@fields;$i++)
			{
				if($fields[$i] eq $desired)
				{
					$header2col{$desired} = $i;
					next ITER;
				}
			}
			die("$desired not in the file:$!");
		} 
		
		$headerProcessed = 1;
	} ## if first row header
	else
	{	
		my $geneid      = $fields[$header2col{'EnsemblGeneID'}];
		my $proteinid   = $fields[$header2col{'EnsemblProteinID'}];
		my $gStart      = $fields[$header2col{'genomicCodingStart'}];  ## 1based
		my $gEnd        = $fields[$header2col{'genomicCodingEnd'}];
		my $chr         = $fields[$header2col{'chr'}];
		my $strand      = $fields[$header2col{'strand'}];
		
		## initialize
		if(!exists($proteinID2aaPos2gPos{$proteinid}))
		{
			$ctr = 0;
			$aaPosCtr = 1;
		}
		
		## store proteinID 2 aaPos 2 genomic Coding Pos
		if($strand == 1)
		{
			for(my $i=$gStart;$i<=$gEnd;$i++)
			{
				$proteinID2aaPos2gPos{$proteinid}{$aaPosCtr}{$ctr} = $i;
				$proteinID2geneID{$proteinid} = $geneid;
				$proteinID2strand{$proteinid} = $strand;
				$ctr++;
			
				if($ctr == 3)
				{
					$aaPosCtr++;
					$ctr = 0;
				}
			}
		}
		elsif($strand == -1)
		{
			for(my $i=$gEnd;$i>=$gStart;$i--)
			{
				$proteinID2aaPos2gPos{$proteinid}{$aaPosCtr}{$ctr} = $i;
				$proteinID2geneID{$proteinid} = $geneid;
				$proteinID2strand{$proteinid} = $strand;
				$ctr++;
				if($ctr == 3)
				{
					$aaPosCtr++;
					$ctr = 0;
				}
			}
		}
		else
		{
			die "unknown 'strand' symbol; either 1 or -1:$!" ;
		}
		
	}
}
close(ENSEM);

##debug
#for my $protID ( sort keys %proteinID2aaPos2gPos ) 
#{
#	for my $aaPos ( sort {$a <=> $b} keys %{$proteinID2aaPos2gPos{$protID}} )
#	{
#		for my $ctr ( sort {$a <=> $b} keys %{$proteinID2aaPos2gPos{$protID}{$aaPos}})
#		{
#			print "$protID\t$aaPos\t$ctr\t$proteinID2aaPos2gPos{$protID}{$aaPos}{$ctr}\n";
#		}
#	}
#}


## input smart domain file
my $ifile = $ARGV[0];
open (INPUT, $ifile) || die "Cannot open $ifile: $!";
print "chr\tstart\tend\tsmartID#startaa#endaa#proteinID#geneID#strand#aaNum#resPos\n";

# variables
my $chr2 = 0;
my $chrflag = 0;
my $headerProcessed2 = 0;
my %header2col2;
my $desired = 0;

while(<INPUT>)
{
	s/\r?\n?$//;
	my @fields2 = split(/\t/, $_);
	chomp @fields2; 
	
	if($headerProcessed2 == 0)
	{
		ITER: for my $desired2 ('chr', 'smartID', 'start', 'end', 'EnsemblProteinID')
		{
			for (my $i=0;$i<@fields2;$i++)
			{
				if($fields2[$i] eq $desired2)
				{
					$header2col2{$desired2} = $i;
					next ITER;
				}
			}
			die("$desired2 can't be in the file:$!");
		} 
		
		$headerProcessed2 = 1;
	} ## if first row header
	else
	{
		my $chromosome = $fields2[$header2col2{'chr'}];
		my $smartID    = $fields2[$header2col2{'smartID'}];
		my $startaa    = $fields2[$header2col2{'start'}];
		my $endaa      = $fields2[$header2col2{'end'}];
		my $proteinID  = $fields2[$header2col2{'EnsemblProteinID'}];
		my $annotation = $smartID . "#" . $startaa . "#" . $endaa . "#" . $proteinID .
		                   "#" . $proteinID2geneID{$proteinID} . "#" . $proteinID2strand{$proteinID};
		
		for (my $i=$startaa;$i<=$endaa;$i++)
		{		
			my $resPos = $i - $startaa + 1;
				
			if($proteinID2strand{$proteinID} == 1)
			{
				
				## this is 1 based
				print "$chromosome\t$proteinID2aaPos2gPos{$proteinID}{$i}{0}\t$proteinID2aaPos2gPos{$proteinID}{$i}{2}\t$annotation".
							"#$i\#$resPos\n";
			}	
			elsif($proteinID2strand{$proteinID} == -1)
			{
				## this is 1 based
				print "$chromosome\t$proteinID2aaPos2gPos{$proteinID}{$i}{2}\t$proteinID2aaPos2gPos{$proteinID}{$i}{0}\t$annotation".
							"#$i\#$resPos\n";
			}
		}
	}
	
}

close(INPUT);
	