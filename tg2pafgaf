#!/usr/bin/perl

use warnings;
use strict;
use fralib;
use File::Basename;
use Getopt::Long;
use Pod::Usage;

=head1 NAME

tg2pafgaf

=head1 SYNOPSIS

 tg2pafgaf [options] tg-file	

  -h help
  -s sample annotation list (required)	
     a)sample-id	
     b)population-id or population-abbreviation
     c)sex (optional)		
  
  -m mk-file (optional)	
     a)snp-id	
     b)chromosome	
     treats all SNPs as autosomal SNPs if this list is not present

  -p alternative column name (optional) 
     toggle the option to use population-abbreviation instead of 
     population-id for population identification;
     0 for population-id (default), 1 for population-abbreviation
     
  --count genotype counts (optional)
          if this is toggled, only the genotype counts will be calculated in .gaf
          (num of geno)/(num of samples excluding missing geno)
  
    example: tg2paf -p 1 -s pscalare.sa pscalare.tg

    Calculates the population allele frequencies and genotype (AA AB BB) frequencies from a tgfile.
    Note that AA plus BB plus AB freq give 1.
    
    Output 2 files:
    (a) .paf file
    (b) .gaf file 
    - rows are SNPs in this order AA, AB, BB, each column is the population
    - Example:
    snp-id	A	B	C
    rs11568732-AA      0.000000        0.000000        0.011236
    rs11568732-AB      0.187500        0.177215        0.089888
    rs11568732-BB      0.812500        0.822785        0.898876

    The genotype AA frequency of rs11568732 in population A is 0; 
    genotype AB frequency 0.1875 and BB is 0.812500.
       
=head1 DESCRIPTION

=cut

#option variables
my $help;
my $saFile;
my $mkFile;
my $tgFile;
my $colNo;
my %label2col;
my $ignoreXChromosome = 0;
my $headerProcessed;
my $popabbrev = 0;
my $popcol;
my $genocount = 0;

#initialize options
Getopt::Long::Configure ('bundling');

if(!GetOptions ('h'=>\$help, 'count'=>\$genocount, 's=s'=>\$saFile, 'm=s'=>\$mkFile, 'p=i'=>\$popabbrev) 
   || !defined($saFile) || scalar(@ARGV)!=1)
{
    if ($help)
    {
        pod2usage(-verbose => 2);
    }
    else
    {
        pod2usage(1);
    }
}

# to toggle between pop-abbrev and pop-id
if ($popabbrev)
{
	$popcol = 'population-abbreviation';
}
else
{
	$popcol = 'population-id';
}


$tgFile = $ARGV[0];
isTg($tgFile) || die "$tgFile not a tgfile";

#data structures
my %SAMPLE;
my %SNP;
my %aaGAF;
my %abGAF;
my %bbGAF;
my %totalSamples;
my %missing;

#read sample annotation file
open(SA, $saFile) || die "Cannot open $saFile";
$headerProcessed = 0;
while(<SA>)
{
	s/\r?\n?$//;
	
	if(!$headerProcessed)
	{
		$colNo = s/\t/\t/g + 1;
		
		my @fields = split('\t', $_, $colNo);
        
		SEARCH_LABEL: for my $label ('sample-id', $popcol, 'sex')
		{
			for my $col (0 .. $#fields)
			{
				if ($fields[$col] eq $label)
				{			
					$label2col{$label} = $col;
					next SEARCH_LABEL;
				}
			}
			
			#'gender' annotation is optional 
			if($label ne 'sex')
			{
				die "Cannot find '$label' in $saFile";
			}
			#ignore gender
			else
			{
			    if(defined($mkFile))
			    {
			        warn "Marker file will be ignored as sex infomation is unavailable in $saFile";  
			    }

			    $ignoreXChromosome = 1;
			}
		}
		
	    if (!$ignoreXChromosome && !defined($mkFile))
		{
		    warn "Sex information will be ignored as mk-file is not supplied";
		    $ignoreXChromosome = 1;
		}
		
		$headerProcessed = 1;
	}
	else
	{
		my @fields = split('\t', $_, $colNo);
		if ($colNo!=scalar(@fields))
		{
		    die "Current row does not have the same number of columns(" . scalar(@fields) . ") as preceding rows($colNo)";
        }
        
		my $sample = $fields[$label2col{'sample-id'}];
		my $population = $fields[$label2col{$popcol}];
		
		$SAMPLE{$sample}{POPULATION} = $population;
		if(!$ignoreXChromosome)
		{
			$SAMPLE{$sample}{SEX} = $fields[$label2col{'sex'}];
		}
	}
}
close(SA);

if(!defined($mkFile))
{
    $ignoreXChromosome = 1;
}

if(!$ignoreXChromosome)
{
	#read snp annotation file
	open(MK, $mkFile) || die "Cannot open $mkFile";
	$headerProcessed = 0;
	while(<MK>)
	{
		s/\r?\n?$//;
		
		##### process header of input file
		if(!$headerProcessed)
		{
			$colNo = s/\t/\t/g + 1;
			
			my @fields = split('\t', $_, $colNo);
        
			SEARCH_LABEL: for my $label ('snp-id', 'chromosome')
			{
				for my $col (0 .. $#fields)
				{
					if ($fields[$col] eq $label)
					{			
						$label2col{$label} = $col;
						next SEARCH_LABEL;
					}
				}
			}
			
			$headerProcessed = 1;
		}
		else
		{
			my @fields = split('\t', $_, $colNo);
			if ($colNo!=scalar(@fields))
    		{
    		    die "Current row does not have the same number of columns(" . scalar(@fields) . ") as preceding rows($colNo)";
            }
        
			my $snp = $fields[$label2col{'snp-id'}];
			my $chromosome = $fields[$label2col{'chromosome'}];
			
			$SNP{$snp}{CHROMOSOME} = $chromosome;
		}
	}
	close(MK);
}

#read genotype file
open(TG, $tgFile) || die "Cannot open $tgFile";
$headerProcessed = 0;

my %POPULATION;
my @sortedPopulations;
my @col2sample;

##### output files
my($name, $path, $ext) = fileparse($tgFile, '\..*');
my $pafFile = "$name.paf";
my $gafFile = "$name.gaf";

open(PAF, ">$pafFile") || die "Cannot open $pafFile";
open(GAF, ">$gafFile") || die "Cannot open $gafFile";

##### TG file
while(<TG>)
{
	s/\r?\n?$//;
	
	##### process output file header
	if(!$headerProcessed)
	{
		$colNo = s/\t/\t/g + 1;
		
		my @fields = split('\t', $_, $colNo);
        
		for my $col (1..$#fields)
		{
			my $sample = $fields[$col];
			$col2sample[$col] = $sample;
			
			if (!defined($POPULATION{$SAMPLE{$sample}{POPULATION}}))
			{
				$POPULATION{$SAMPLE{$sample}{POPULATION}} = ();
			}
			
			push(@{$POPULATION{$SAMPLE{$sample}{POPULATION}}}, $col);
		}
		
		@sortedPopulations = sort(keys(%POPULATION));
		
		print PAF "snp-id\t" . join("\t", @sortedPopulations) . "\n";
		print GAF "snp-id\t" . join("\t", @sortedPopulations) . "\n";
		
		$headerProcessed = 1;
	}
	else # header processed
	{
		my @fields = split('\t', $_, $colNo);
		if ($colNo!=scalar(@fields))
		{
		    die "Current row does not have the same number of columns(" . scalar(@fields) . ") as preceding rows($colNo)";
        }
        
		my $snp = $fields[0];
		
		print PAF $snp;
		print GAF $snp;
		
		if(!$ignoreXChromosome && $SNP{$snp}{CHROMOSOME} eq 'X')
		{
			for my $pop (@sortedPopulations)
			{
				my $count = 0;
				my $total = 0;
				
				my $AA = 0;
				my $AB = 0;
				my $BB = 0;
				my $missing = 0;
				
				for my $col (@{$POPULATION{$pop}})
				{
					my $genotype = $fields[$col];
					
					if ($genotype!=-1)
					{
						if ($SAMPLE{$col2sample[$col]}{SEX} eq 'male')
						{
							$count += $genotype>>1;
							$total += 1;
						}
						else
						{
							$count += $genotype;
							$total += 2;
							
							# calc genotype frequency gaf
							# count the number of AA and AB and BB
							if ($genotype==0)
							{
								$AA++;
							}
							elsif ($genotype==1)
							{
								$AB++;
							}
							else
							{
								$BB++;
							}
						}
					}
					else
					{
						$missing++;
					}
				}

				if ($total==0)
				{
					print PAF "\tn/a";
					$aaGAF{$pop} = "-1";
					$abGAF{$pop} = "-1";
					$bbGAF{$pop} = "-1";
					warn "No known alleles for frequency calculation: $pop, $snp";
				}
				else
				{
					my $paf = $count/$total;
					$aaGAF{$pop} = $AA/($total/2);
					$abGAF{$pop} = $AB/($total/2);
					$bbGAF{$pop} = $BB/($total/2);
										
					printf PAF "\t%.6f", $paf;
				}
			}
			
			print PAF "\n";
		} # if autosomes
		else
		{
			for my $pop (@sortedPopulations)
			{
				my $count = 0;
				my $total = 0;
				
				my $AA = 0;
				my $AB = 0;
				my $BB = 0;
				my $missing = 0;
				
				for my $col (@{$POPULATION{$pop}})
				{
					my $genotype = $fields[$col];
					
					if ($genotype!=-1) ## if non-missing
					{
						$count += $genotype;
						$total += 2;
						
						# calc genotype frequency gaf
						# count the number of AA and AB and BB
						if ($genotype==0)
						{
							$AA++;
						}
						elsif ($genotype==1)
						{
							$AB++;
						}
						else
						{
							$BB++;
						}
					}
					else
					{
						$missing++;
					}
				}
				
				if ($total==0) ## if all missing
				{
					print PAF "\tn/a";
					$aaGAF{$pop} = "-1";
					$abGAF{$pop} = "-1";
					$bbGAF{$pop} = "-1";
					warn "No valid genotypes for $snp in $pop";
				}
				else
				{
					my $paf = $count/$total;
					$totalSamples{$pop} = $total/2;
					
					if(!$genocount)
					{
						$aaGAF{$pop} = $AA/($total/2);
						$abGAF{$pop} = $AB/($total/2);
						$bbGAF{$pop} = $BB/($total/2);
					}
					else
					{
						$aaGAF{$pop} = $AA;
						$abGAF{$pop} = $AB;
						$bbGAF{$pop} = $BB;
						$missing{$pop} = $missing;
					}
										
					printf PAF "\t%.6f", $paf;
				}
			}
			print PAF "\n";
		} #autosomes 
		
		##### print output in GAF file
		print GAF "-AA";
		
		foreach my $i (@sortedPopulations)
		{
			my $grandtotal = $totalSamples{$i} + $missing{$i};
			if ($aaGAF{$i} == "-1")
			{
				print GAF "\t0/$grandtotal";
				#print GAF "\tn\/a";
			}
			else
			{
				if(!$genocount)
				{
					printf GAF "\t%.6f", $aaGAF{$i};
				}
				else
				{
					printf GAF "\t$aaGAF{$i}/$grandtotal";
				}
			}
		}
		print GAF "\n";
		print GAF "$snp-AB";
		foreach my $i (@sortedPopulations)
		{
			my $grandtotal = $totalSamples{$i} + $missing{$i};
			if ($abGAF{$i} == "-1")
			{
				print GAF "\t0/$grandtotal";
				#print GAF "\tn\/a";
			}
			else
			{
				if(!$genocount)
				{
					printf GAF "\t%.6f", $abGAF{$i};
				}
				else
				{
					
					printf GAF "\t$abGAF{$i}/$grandtotal";
				}
			}
		}
		print GAF "\n";
		print GAF "$snp-BB";
		foreach my $i (@sortedPopulations)
		{
			my $grandtotal = $totalSamples{$i} + $missing{$i};
			if ($bbGAF{$i} == "-1")
			{
				print GAF "\t0/$grandtotal";
				#print GAF "\tn\/a";
			}
			else
			{
				if(!$genocount)
				{
					printf GAF "\t%.6f", $bbGAF{$i};
				}
				else
				{
					
					printf GAF "\t$bbGAF{$i}/$grandtotal";
				}
			}
		}
		print GAF "\n";
		
	} #header processed	
} # TG file; 1 SNP 1 loop


close(TG);
close(GAF);
close(PAF);
